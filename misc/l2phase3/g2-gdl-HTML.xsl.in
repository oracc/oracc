<?xml version='1.0' encoding='utf-8'?>

<xsl:stylesheet version="1.0" 
  xmlns:g="http://oracc.org/ns/gdl/1.0"
  xmlns:xtf="http://oracc.org/ns/xtf/1.0"
  xmlns:xcl="http://oracc.org/ns/xcl/1.0"
  xmlns:xff="http://oracc.org/ns/xff/1.0"
  xmlns="http://www.w3.org/1999/xhtml"
  xmlns:norm="http://oracc.org/ns/norm/1.0"
  xmlns:note="http://oracc.org/ns/note/1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:xl="http://www.w3.org/1999/xlink"
  exclude-result-prefixes="g norm xcl xff xtf xl">

<xsl:output method="xml" indent="yes" encoding="utf-8"/>

<xsl:include href="xpd.xsl"/>
<xsl:include href="gdl-render-g.xsl"/>

<xsl:param name="render-mister" select="'yes'"/>

<xsl:variable name="config-file" select="concat('@@ORACC@@/xml/',$project,'/config.xml')"/>

<xsl:param name="render-accents">
  <xsl:call-template name="xpd-option">
    <xsl:with-param name="config-xml" select="$config-file"/>
    <xsl:with-param name="option" select="'render-accents'"/>
  </xsl:call-template>
</xsl:param>

<xsl:param name="render-inter-det-char">
  <xsl:call-template name="xpd-option">
    <xsl:with-param name="config-xml" select="$config-file"/>
    <xsl:with-param name="option" select="'render-inter-det-char'"/>
    <xsl:with-param name="default" select="''"/>
  </xsl:call-template>
</xsl:param>

<xsl:variable name="word-absent-char" select="'&#x2014;'"/>
<xsl:variable name="word-broken-char" select="'&#x2003;'"/> <!-- U+2003 = EM SPACE -->
<xsl:variable name="word-linefrom-char" select="'&#x2190;'"/>
<xsl:variable name="word-linecont-char" select="'&#x2192;'"/>

<xsl:key name="lnodes" match="xcl:l" use="@ref"/>
<xsl:key name="xnodes" match="xcl:link" use="substring(@xl:href,2)"/>

<xsl:template match="g:w">
  <xsl:param name="allow-space" select="not(@headform)"/>
  <xsl:param name="proofing-mode" select="false()"/>
  <xsl:call-template name="gdl-w">
    <xsl:with-param name="allow-space" select="$allow-space"/>
    <xsl:with-param name="proofing-mode" select="$proofing-mode"/>
  </xsl:call-template>
  <xsl:if test="@headform"><xsl:text>-</xsl:text></xsl:if>
</xsl:template>

<xsl:template match="norm:w">
  <xsl:call-template name="gdl-w">
    <xsl:with-param name="allow-space" select="true()"/>
    <xsl:with-param name="proofing-mode" select="false()"/>
  </xsl:call-template>
</xsl:template>

<xsl:template match="g:swc">
  <!--<xsl:text>-</xsl:text>--> <!--removed in attempt to fix duplicate hyphen on split word-->
  <xsl:choose>
    <xsl:when test="@swc-final = '0'">
      <xsl:call-template name="gdl-w">
	<xsl:with-param name="allow-space" select="false()"/>
      </xsl:call-template>
      <xsl:text>-</xsl:text>
    </xsl:when>
    <xsl:otherwise>
      <xsl:call-template name="gdl-w">
	<xsl:with-param name="allow-space" select="true()"/>
      </xsl:call-template>      
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<!--
<xsl:template name="render-g">
  <xsl:choose>
    <xsl:when test="$render-accents = 'yes' and @g:accented">
      <xsl:value-of select="@g:accented"/>
      <xsl:if test="g:b">
	<xsl:apply-templates select="g:m|g:a"/>
      </xsl:if>
    </xsl:when>
    <xsl:otherwise>
      <xsl:apply-templates/>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>
-->

<xsl:template name="set-lang">
  <!--<xsl:message>set-lang fwhost=<xsl:value-of select="ancestor-or-self::g:w/@fwhost"/></xsl:message>-->
  <xsl:variable name="xml-lang">
    <xsl:value-of select="ancestor-or-self::*[@xml:lang][1]/@xml:lang"/>
  </xsl:variable>
  <xsl:variable name="fwhost">
    <xsl:if test="ancestor-or-self::g:w/@fwhost">
      <xsl:value-of select="concat(ancestor-or-self::g:w/@fwhost,'-')"/>
    </xsl:if>
  </xsl:variable>
  <!--<xsl:message>$fwhost=<xsl:value-of select="$fwhost"/></xsl:message>-->
  <xsl:choose>
    <xsl:when test="contains($xml-lang,'-')">
      <xsl:value-of select="concat($fwhost,substring-before($xml-lang,'-'))"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:value-of select="concat($fwhost,$xml-lang)"/>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:t">
  <xsl:value-of select="text()"/>
</xsl:template>

<xsl:template match="g:v|norm:s">
  <xsl:call-template name="render-o"/>
  <xsl:variable name="lang">
    <xsl:call-template name="set-lang"/>
  </xsl:variable>
  <xsl:choose>
    <xsl:when test="ancestor::g:d">
      <sup>
	<xsl:choose>
	  <xsl:when test="ancestor::g:d[@g:role='phonetic']">
	    <xsl:attribute name="class">
	      <xsl:value-of select="$lang"/>
	    </xsl:attribute>
	  </xsl:when>
	  <xsl:otherwise>
	    <xsl:attribute name="class">
	      <xsl:text>sux</xsl:text>
	    </xsl:attribute>
	  </xsl:otherwise>
	</xsl:choose>
	<xsl:call-template name="render-g"/>
	<xsl:call-template name="render-flags"/>
      </sup>
    </xsl:when>
    <xsl:when test="self::norm:s">
      <span x="6">
	<xsl:attribute name="class">
	  <xsl:choose>
	    <xsl:when test="@norm:num='yes'">
	      <xsl:value-of select="'r'"/>
	    </xsl:when>
	    <xsl:otherwise>
	      <xsl:value-of select="$lang"/>
	    </xsl:otherwise>
	  </xsl:choose>
	</xsl:attribute>
	<xsl:call-template name="render-g"/>
      </span>
      <xsl:call-template name="render-flags"/>
    </xsl:when>
    <xsl:otherwise>
      <span x="3">
	<xsl:attribute name="class">
	  <xsl:value-of select="$lang"/>
	</xsl:attribute>
	<xsl:call-template name="render-g"/>
      </span>
      <xsl:call-template name="render-flags"/>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:call-template name="render-c"/>
</xsl:template>

<xsl:template match="g:s">
  <!-- This is needed because TIN.TIR{KI} renders as TIN.TIR.KI but there is no @g:delim on the g:s -->
  <xsl:if test="ancestor::g:d/@g:pos = 'post'">.</xsl:if>
  <xsl:call-template name="render-o"/>
  <xsl:choose>
    <xsl:when test="ancestor::g:d and ancestor::g:q">
      <sup>
	<span class="sign {@g:logolang}">
	  <xsl:apply-templates/>
	</span>
	<xsl:call-template name="render-flags"/>
      </sup>
    </xsl:when>
    <xsl:when test="ancestor-or-self::g:w/@xml:lang = 'hit' and ancestor::g:d">
      <sup>
	<span class="sign {@g:logolang}">
	  <xsl:apply-templates/>
	</span>
	<xsl:call-template name="render-flags"/>
      </sup>
    </xsl:when>
    <xsl:otherwise>
      <span class="sign {@g:logolang}">
	<xsl:call-template name="render-g"/>
      </span>
      <xsl:call-template name="render-flags"/>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:call-template name="render-c"/>
  <!-- This is necessary for {KUR}TIN.TIR -> KUR.TIN.TIR because no @g:delim on these nodes -->
  <xsl:if test="not(ancestor::g:q) and ancestor::g:d 
		and not(ancestor::g:d/@g:pos = 'post')"
		>.</xsl:if>
</xsl:template>

<xsl:template match="g:d">
  <xsl:call-template name="render-o"/>
  <xsl:for-each select="*">
    <xsl:apply-templates select="."/>
  </xsl:for-each>
  <xsl:call-template name="render-flags"/>
  <xsl:call-template name="render-c"/>
  <xsl:if test="local-name(following::*[1])='d'">
<!--    <xsl:message>Adjacent g:d with roles <xsl:value-of select="@g:role"/> and <xsl:value-of select="following::*[1]/@g:role"/></xsl:message>-->
    <xsl:choose>
      <xsl:when test="not(@g:role = following::*[1]/@g:role)">
<!--	<xsl:message>emitting space</xsl:message>-->
	<xsl:text>&#x202f;</xsl:text>
      </xsl:when>
      <xsl:otherwise>
	<xsl:value-of select="$render-inter-det-char"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:if>
</xsl:template>

<xsl:template match="g:r">
<!--  <xsl:message>g:r=<xsl:value-of select="."/></xsl:message> -->
  <xsl:value-of select="."/>
</xsl:template>

<xsl:template match="g:x">
  <xsl:choose>
    <xsl:when test="@g:type = 'newline'">
      <xsl:choose>
	<xsl:when test="preceding-sibling::g:w">
	  <xsl:text>&#xa0;; </xsl:text>
	</xsl:when>
	<xsl:otherwise>
	  <xsl:text>;</xsl:text>
	</xsl:otherwise>
      </xsl:choose>
    </xsl:when>
    <xsl:when test="@g:type = 'linebreak'">
      <br/>
    </xsl:when>
    <xsl:when test="@g:type = 'disamb'">
      <xsl:value-of select="text()"/>
    </xsl:when>
    <xsl:otherwise>
      <span class="r">
	<xsl:value-of select="@g:o"/>
	<xsl:choose>
	  <xsl:when test="@g:type='word-absent'">
	    <xsl:value-of select="$word-absent-char"/>
	  </xsl:when>
	  <xsl:when test="@g:type='word-broken'">
	    <xsl:value-of select="$word-broken-char"/>
	  </xsl:when>
	  <xsl:when test="@g:type='word-linefrom'">
	    <xsl:value-of select="$word-linefrom-char"/>
	  </xsl:when>
	  <xsl:when test="@g:type='word-linecont'">
	    <xsl:value-of select="$word-linecont-char"/>
	  </xsl:when>
	  <xsl:when test="@g:type='dollar' or @g:type='comment'">
	    <xsl:value-of select="concat('(',text(),')')"/>
	  </xsl:when>
	  <xsl:otherwise>
	    <xsl:value-of select="text()"/>
	  </xsl:otherwise>
	</xsl:choose>
	<xsl:call-template name="render-c"/>
      </span>
    </xsl:otherwise>
  </xsl:choose>
<!-- This is in render-word now so shouldn't be needed here
  <xsl:if test="@notemark">
    <xsl:call-template name="process-notes"/>
  </xsl:if>
 -->
</xsl:template>

<xsl:template match="g:b">
  <xsl:choose>
    <xsl:when test="g:o">
      <xsl:text>(</xsl:text>
      <xsl:apply-templates/>
      <xsl:text>)</xsl:text>
    </xsl:when>
    <xsl:otherwise>
      <xsl:apply-templates/>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:p">
  <xsl:call-template name="render-o"/>
  <xsl:value-of select="@g:type"/>
  <xsl:if test="*">
    <xsl:text>(</xsl:text>
    <xsl:apply-templates/>
    <xsl:text>)</xsl:text>
  </xsl:if>
  <xsl:call-template name="render-flags"/>
  <xsl:call-template name="render-c"/>
  <xsl:text> </xsl:text>
</xsl:template>

<xsl:template match="g:q">
  <xsl:call-template name="render-o"/>
  <xsl:apply-templates select="*[1]"/>
  <xsl:choose>
    <xsl:when test="ancestor::g:d">
      <sup><xsl:text>(</xsl:text></sup>
    </xsl:when>
    <xsl:otherwise>
      <xsl:text>(</xsl:text>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:apply-templates select="*[2]"/>
  <xsl:choose>
    <xsl:when test="ancestor::g:d">
      <sup><xsl:text>)</xsl:text></sup>
    </xsl:when>
    <xsl:otherwise>
      <xsl:text>)</xsl:text>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:call-template name="render-flags"/>
  <xsl:call-template name="render-c"/>
</xsl:template>

<xsl:template match="g:a">
  <xsl:choose>
    <xsl:when test="ancestor-or-self::*[@xml:lang][1]/@xml:lang='qpc'">
      <sub><xsl:apply-templates/></sub>
    </xsl:when>
    <xsl:otherwise>
      <sup><xsl:apply-templates/></sup>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:f"/>

<xsl:template match="g:m">
  <sup>
    <xsl:apply-templates/>
  </sup>
<!--
  <sub>
    <xsl:text>@</xsl:text>
    <xsl:apply-templates/>
  </sub>
 -->
</xsl:template>

<xsl:template match="g:n">
  <xsl:call-template name="render-o"/>
  <xsl:choose>
    <xsl:when test="ancestor::g:d">
      <sup>
	<xsl:if test="ancestor::g:d[@g:role='phonetic']">
	  <xsl:attribute name="class">
	    <xsl:call-template name="set-lang"/>
	  </xsl:attribute>
	</xsl:if>
	<xsl:choose>
	  <xsl:when test="g:r = '1' and $render-mister = 'yes'
			  and (count(*) = 1 or *[2] = 'diš')">
	    <xsl:text>m</xsl:text>
	  </xsl:when>
	  <xsl:otherwise>
	    <xsl:call-template name="render-n"/>
	  </xsl:otherwise>
	</xsl:choose>
	<xsl:apply-templates select="g:a|g:m"/>
	<xsl:call-template name="render-flags"/>
      </sup>
    </xsl:when>
    <xsl:otherwise>
      <xsl:call-template name="render-n"/>
      <xsl:apply-templates select="g:a|g:m"/>
      <xsl:call-template name="render-flags"/>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:call-template name="render-c"/>
</xsl:template>

<xsl:template name="render-n">
  <!-- <xsl:message>render-n</xsl:message> -->
  <xsl:choose>
    <xsl:when test="g:b">
      <xsl:for-each select="g:b">
        <xsl:apply-templates select="g:r"/>
        <xsl:text>(</xsl:text>
        <xsl:apply-templates select="*[2]"/>
	<xsl:apply-templates select="g:a|g:m"/>
        <xsl:text>)</xsl:text>
      </xsl:for-each>
     </xsl:when>
    <xsl:otherwise>
      <xsl:apply-templates select="g:r"/>
      <xsl:if test="count(*[not(self::g:m) and not(self::g:a)])>1">
	<xsl:text>(</xsl:text>
	<xsl:apply-templates select="*[2]"/>
	<xsl:text>)</xsl:text>
      </xsl:if>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:c">
  <xsl:call-template name="render-o"/>
  <span class="compound">
    <xsl:apply-templates/>
    <xsl:call-template name="render-flags"/>
  </span>
  <xsl:call-template name="render-c"/>
</xsl:template>

<xsl:template match="g:g">
  <xsl:choose>
    <xsl:when test="g:b">
      <xsl:apply-templates/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:text>(</xsl:text>
      <xsl:apply-templates/>
      <xsl:text>)</xsl:text>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:o">
  <xsl:choose>
    <xsl:when test="@g:type='beside'"><xsl:text>.</xsl:text></xsl:when>
    <xsl:when test="@g:type='joining'"><xsl:text>+</xsl:text></xsl:when>
    <xsl:when test="@g:type='containing'"><xsl:text>×</xsl:text></xsl:when>
    <xsl:when test="@g:type='above'"><xsl:text>&amp;</xsl:text></xsl:when>
    <xsl:when test="@g:type='crossing'"><xsl:text>%</xsl:text></xsl:when>
    <xsl:when test="@g:type='opposing'"><xsl:text>@</xsl:text></xsl:when>
    <xsl:when test="@g:type='reordered'"><xsl:text>:</xsl:text></xsl:when>
    <xsl:when test="@g:type='repeated'">
      <xsl:value-of select="concat(.,'x')"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:message>g2-gdl-HTML: operator <xsl:value-of select="@g:type"/> not handled</xsl:message>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:gg">
  <xsl:choose>
    <xsl:when test="@g:type='correction'">
      <xsl:apply-templates select="*[1]"/>
      <xsl:text>(</xsl:text>
	<xsl:for-each select="*[position()>1]">
	  <xsl:apply-templates select="."/>
	  <xsl:if test="not(position()=last())">
	    <!--<xsl:text>-</xsl:text>-->
	    <xsl:value-of select="@g:delim"/>
          </xsl:if>
 	</xsl:for-each>
      <xsl:text>)</xsl:text>
    </xsl:when>
    <xsl:when test="@g:type='alternation'">
      <xsl:for-each select="*">
	<xsl:apply-templates select="."/>
      </xsl:for-each>
    </xsl:when>
    <xsl:when test="@g:type='ligature'">
      <xsl:for-each select="*">
	<xsl:apply-templates select="."/>
	  <xsl:choose>
	    <xsl:when test="text()='x'">
	      <xsl:choose>
		<xsl:when test="following-sibling::*[1][self::g:d]"/>
		<xsl:otherwise>
		  <xsl:choose>
		    <xsl:when test="@g:delim='+'">
		      <!-- <xsl:text> </xsl:text> -->
		    </xsl:when>
		    <xsl:otherwise>
		      <!--<xsl:value-of select="@g:delim"/>-->
		    </xsl:otherwise>
		  </xsl:choose>
		</xsl:otherwise>
	      </xsl:choose>
	    </xsl:when>
	    <xsl:otherwise>
	      <!-- <xsl:value-of select="@g:delim"/> -->
	    </xsl:otherwise>
	  </xsl:choose>
      </xsl:for-each>
    </xsl:when>
    <xsl:when test="@g:type='implicit-ligature'">
      <xsl:apply-templates/>
    </xsl:when>
    <xsl:when test="@g:type='group' or @g:type='logo'">
      <xsl:for-each select="*">
	<xsl:apply-templates select="."/>
	<xsl:choose>
	  <xsl:when test="self::g:d[@g:pos='pre']
			  or following-sibling::*[1][self::g:d[@g:pos='post']]"/>
	  <xsl:otherwise>
	    <xsl:choose>
	      <xsl:when test="@g:delim='--'">
		<!--<xsl:text>—</xsl:text>-->
	      </xsl:when>
	      <xsl:otherwise>
		<!--<xsl:value-of select="@g:delim"/>-->
	      </xsl:otherwise>
	    </xsl:choose>
	  </xsl:otherwise>
	</xsl:choose>
      </xsl:for-each>
    </xsl:when>
    <xsl:when test="@g:type='reordering'">
      <xsl:for-each select="*">
	<xsl:apply-templates select="."/>
	<xsl:if test="not(position()=last())">
	  <xsl:text>:</xsl:text>
        </xsl:if>
      </xsl:for-each>
    </xsl:when>
    <xsl:when test="@g:type='diszless'">
      <xsl:value-of select="g:t"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:message>g2-gdl-HTML: group type <xsl:value-of select="@g:type"/> not handled</xsl:message>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:surro">
  <xsl:apply-templates select="*[1]"/>
  <xsl:text>&lt;(</xsl:text>
  <xsl:apply-templates select="*[2]"/>
  <xsl:text>)></xsl:text>
</xsl:template>

<xsl:template match="g:nonw">
  <xsl:choose>
    <xsl:when test="@type='vari'">
      <sup class="varmark"><xsl:value-of select="concat('(:',.,':)')"/></sup>
    </xsl:when>
    <xsl:when test="contains(.,'_')">
      <xsl:value-of select="translate(.,'_','&#xa0;')"/>
    </xsl:when>
    <xsl:when test="../self::xtf:surro or ../self::g:gsurro">
      <xsl:apply-templates/>
    </xsl:when>
    <xsl:when test="@type='punct' or @type='excised'">
      <xsl:for-each select="*">
	<xsl:apply-templates select="."/>
	<!--<xsl:value-of select="@g:delim"/>-->
      </xsl:for-each>
      <xsl:choose>
	<xsl:when test="@g:delim">
	  <xsl:value-of select="@g:delim"/>
	</xsl:when>
	<xsl:otherwise>
	  <xsl:text> </xsl:text>
	</xsl:otherwise>
      </xsl:choose>
    </xsl:when>
    <xsl:when test="@type='comment'"/>
    <xsl:when test="starts-with(g:x/@g:type,'word-')">
      <xsl:apply-templates/>
    </xsl:when>
    <xsl:when test="@type='notelink'">
      <xsl:call-template name="process-notes"/>
      <xsl:text> </xsl:text>
    </xsl:when>
    <xsl:otherwise>
      <xsl:text> (</xsl:text>
      <xsl:apply-templates/>
      <xsl:text>) </xsl:text>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<!-- Hacks to work around either JS escaping or JS quoting issues -->
<xsl:variable name="quote"><xsl:text>+'\</xsl:text></xsl:variable>
<xsl:variable name="acute" select="'&#x00B1;&#x00B4;&#x20E5;'"/>

<xsl:template name="render-g-text">
  <xsl:param name="t"/>
  <xsl:value-of select="$t"/>
</xsl:template>

<!--
<xsl:template name="x-render-g-text">
  <xsl:param name="t"/>
  <xsl:variable name="body" select="translate($t,$subdig,'')"/>
  <xsl:value-of select="$body"/>
  <xsl:if test="string-length($body) &lt; string-length($t)">
    <sub>
      <xsl:value-of select="translate(substring($t,1+string-length($body)),
			    $subdig,$regdig)"/>
    </sub>
  </xsl:if>
</xsl:template>
-->

<xsl:template name="render-flags">
  <xsl:if test="@g:collated
		or @g:queried
		or @g:remarked">
    <sup class="sux">
      <xsl:if test="@g:collated"><xsl:text>*</xsl:text></xsl:if>
      <xsl:if test="@g:remarked"><xsl:text>!</xsl:text></xsl:if>
      <xsl:if test="@g:queried"><xsl:text>?</xsl:text></xsl:if>
    </sup>
  </xsl:if>
  <xsl:if test="@g:uflag1"><sup class="sux"><xsl:text>&#x2020;</xsl:text></sup></xsl:if>
  <xsl:if test="@g:uflag2"><sup class="sux"><xsl:text>&#x2021;</xsl:text></sup></xsl:if>
  <xsl:if test="@g:uflag3"><sup class="sux"><xsl:text>&#x2022;</xsl:text></sup></xsl:if>
  <xsl:if test="@g:uflag4"><sup class="sux"><xsl:text>&#x2023;</xsl:text></sup></xsl:if>
</xsl:template>

<xsl:template name="render-o">
  <xsl:if test="(string-length(@g:varo)+string-length(@g:o)+string-length(@g:ho)) > 0">
    <span class="sux">
      <xsl:if test="@g:varo">
	<sup class="varmark"><xsl:value-of select="concat('(',@g:varo,':')"/></sup>
      </xsl:if>
      <xsl:value-of select="@g:o"/>
      <xsl:if test="@g:ho">
	<xsl:text>&#x2e22;</xsl:text>
      </xsl:if>
    </span>
  </xsl:if>
</xsl:template>

<xsl:template name="render-c">
  <xsl:if test="(string-length(@g:varc)+string-length(@g:c)+string-length(@g:hc)) > 0">
    <span class="sux">
      <xsl:if test="@g:hc">
	<xsl:text>&#x2e23;</xsl:text>
      </xsl:if>
      <xsl:value-of select="@g:c"/>
      <xsl:if test="@g:varc">
	<sup class="varmark"><xsl:value-of select="concat(':',@g:varc,')')"/></sup>
      </xsl:if>
    </span>
  </xsl:if>
  <xsl:value-of select="@g:delim"/>
</xsl:template>

<xsl:template name="gdl-w">
  <xsl:param name="allow-space" select="true()"/>
  <xsl:param name="proofing-mode" select="false()"/>

  <xsl:variable name="lang">
    <xsl:call-template name="set-lang"/>
  </xsl:variable>
  <xsl:variable name="lnodes" select="key('lnodes',@xml:id)"/>
  <xsl:variable name="xnode" select="key('xnodes',$lnodes[1]/@xml:id)"/>

  <xsl:variable name="form-id">
    <xsl:choose>
      <xsl:when test="$xnode/xl:role='psu'"><xsl:value-of select="$xnode/../@xml:id"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="$lnodes[1]/@xml:id"/></xsl:otherwise>
    </xsl:choose>
  </xsl:variable>

  <xsl:variable name="lemnode" select="id($form-id)"/>
  <xsl:variable name="lemform" select="$lemnode/xff:f"/>
  <xsl:variable name="lemdict" select="$lemform/following-sibling::*[1]"/>

  <xsl:variable name="ltitle">
    <xsl:choose>
      <xsl:when test="$lemnode/@sig">
	<xsl:value-of select="substring-after($lemnode/@sig,'=')"/>
      </xsl:when>
      <xsl:when test="$lemnode/@exosig">
	<xsl:value-of select="substring-after($lemnode/@exosig,'=')"/>
      </xsl:when>
      <xsl:when test="$lemform">
	<xsl:value-of select="concat($lemform/@norm0,
			      '[',$lemform/@sense,']',
			      $lemform/@epos)"/>
      </xsl:when>
    </xsl:choose>
  </xsl:variable>
<!--
  <xsl:choose>
    <xsl:when test="$proofing-mode">
      <xsl:call-template name="gdl-w-table">
	<xsl:with-param name="lemform" select="$lemform"/>
      </xsl:call-template>
    </xsl:when>
    <xsl:otherwise>
      <xsl:call-template name="gdl-w-span">
	<xsl:with-param name="lemdict" select="$lemdict"/>
	<xsl:with-param name="ltitle" select="$ltitle"/>
      </xsl:call-template>
    </xsl:otherwise>
  </xsl:choose>
 -->
  <xsl:call-template name="gdl-w-span">
    <xsl:with-param name="lemdict" select="$lemdict"/>
    <xsl:with-param name="lemnode" select="$lemnode"/>
    <xsl:with-param name="lemform" select="$lemform"/>
    <xsl:with-param name="ltitle" select="$ltitle"/>
  </xsl:call-template>
  <xsl:if test="$allow-space and following-sibling::* and not(following-sibling::*[1][@type='notelink'])">
    <xsl:text> </xsl:text>
  </xsl:if>
</xsl:template>

<!--FIXME: initial implementation, no support for norm:w or COFs-->
<xsl:template name="gdl-w-table">
  <xsl:param name="lemform"/>
  <table class="w" id="{@xml:id}">
    <tr><td><xsl:call-template name="render-word"/></td></tr>
    <tr><td><xsl:value-of select="$lemform/@sublem"/></td></tr>
    <xsl:choose>
      <xsl:when test="$lemform/@morph2">
	<tr><td><xsl:value-of select="$lemform/@morph"/></td></tr>
	<tr><td><xsl:value-of select="$lemform/@morph2"/></td></tr>
      </xsl:when>
      <xsl:otherwise>
	<tr><td><xsl:value-of select="$lemform/@norm0"/></td></tr>
      </xsl:otherwise>
    </xsl:choose>
  </table>
</xsl:template>

<xsl:template name="gdl-w-span">
  <xsl:param name="lemnode"/>
  <xsl:param name="lemdict"/>
  <xsl:param name="lemform"/>
  <xsl:param name="ltitle"/>

  <xsl:variable name="w-class">
    <xsl:choose>
      <xsl:when test="contains(@class,'selected')">
	<xsl:text>w selected</xsl:text>
      </xsl:when>
      <xsl:otherwise>
	<xsl:text>w</xsl:text>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="lsig" select="$lemnode/@sig|$lemnode/@exosig"/>
  <xsl:variable name="pos" select="$lemnode/*[1]/@pos"/>
  <xsl:variable name="cspos">
    <xsl:choose>
      <xsl:when test="$pos='N'">NOUN</xsl:when>
      <xsl:otherwise><xsl:value-of select="$pos"/></xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <!--  <xsl:message><xsl:value-of select="$lsig"/></xsl:message> -->
  <span x="1" class="{$w-class} {@xml:lang} {$cspos}" id="{@xml:id}">
    <xsl:variable name="wid" select="@xml:id"/>
    <xsl:variable name="xsig">
      <xsl:choose>
	<xsl:when test="contains($lsig,' += ')">
	  <xsl:value-of select="concat('{', substring-after($lsig,' += '))"/>
	</xsl:when>
	<xsl:otherwise>
	  <xsl:value-of select="$lsig"/>
	</xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    <xsl:variable name="sig" select="translate($xsig,$quote,$acute)"/>
    <xsl:choose>
      <xsl:when test="self::norm:w">
	<xsl:call-template name="render-o"/>
	<xsl:choose>
	  <xsl:when test="string-length($sig) > 0">
	    <a class="cbd" 
	       href="javascript://" onclick="act_wsig(event)" data-wsig="&#x2623;{$sig}">
	      <xsl:call-template name="render-word"/>
	      <xsl:call-template name="render-flags"/>
	      <xsl:call-template name="render-c"/>
	    </a>
	  </xsl:when>
	  <xsl:otherwise>
	    <xsl:call-template name="render-word"/>
	    <xsl:call-template name="render-flags"/>
	    <xsl:call-template name="render-c"/>
	  </xsl:otherwise>
	</xsl:choose>
      </xsl:when>
      <xsl:when test="string-length($sig) > 0">
	<xsl:if test="string-length($ltitle) > 0">
	  <xsl:attribute name="title">
	    <xsl:value-of select="$ltitle"/>
	  </xsl:attribute>
	</xsl:if>
	<a class="cbd" 
	   href="javascript://" onclick="act_wsig(event)" data-wsig="&#x2623;{$sig}">
	  <xsl:call-template name="render-word"/>
	</a>
      </xsl:when>
     <xsl:otherwise>
       <xsl:call-template name="render-word"/>
     </xsl:otherwise>
    </xsl:choose>
  </span>
  <xsl:if test="self::norm:w">
    <span x="11">
      <xsl:value-of select="@g:delim"/>
    </span>
  </xsl:if>
</xsl:template>

<xsl:template name="render-word">
  <xsl:for-each select="*">
    <xsl:apply-templates select="."/>
<!--
    <xsl:if test="@notemark">
      <xsl:call-template name="process-notes"/>
    </xsl:if>
 -->
    <xsl:choose>
      <xsl:when test="./self::g:d and ./following-sibling::*[1][self::g:d]">
<!--	<xsl:message>Adjacent g:d with roles <xsl:value-of select="@g:role"/> and <xsl:value-of select="following-sibling::*[1]/@g:role"/></xsl:message>-->
	<xsl:choose>
	  <xsl:when test="not(self::g:d/@g:role = following-sibling::*[1]/@g:role)">
	    <xsl:text> </xsl:text>
	  </xsl:when>
	  <xsl:otherwise>
	    <xsl:value-of select="$render-inter-det-char"/>
	  </xsl:otherwise>
	</xsl:choose>
	<!--<xsl:text>&#xa0;</xsl:text>-->
      </xsl:when>
      <xsl:otherwise>
	<!--	<xsl:call-template name="maybe-hyphen"/> -->
<!--
	<xsl:if test="not(@g:delim='- -')">
	  <xsl:value-of select="@g:delim"/>
	</xsl:if>
-->
      </xsl:otherwise>
    </xsl:choose>
  </xsl:for-each>
</xsl:template>

<!-- This is the note mark in the transliteration line of XTF data -->
<xsl:template name="process-notes">
  <xsl:choose>
    <xsl:when test="$proofing-mode = true()">
    </xsl:when>
    <xsl:otherwise>
      <a href="javascript://" 
	 onmouseover="showNote(event,'{@note:ref}')" 
	 onmouseout="hideNote(event,'{@note:ref}')">
	<span class="marker">
	  <xsl:apply-templates/>
	</span>
      </a>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="g:gloss">
  <sup>
    <xsl:apply-templates/>
  </sup>
</xsl:template>

<xsl:template match="g:*">
  <xsl:message>g2-gdl-HTML: <xsl:value-of select="name()"/> not handled</xsl:message>
  <xsl:apply-templates/>
</xsl:template>

</xsl:stylesheet>
